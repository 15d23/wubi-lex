import config;
import process;
import com;

namespace app;

systemStartup = function(taskName,description,path,arguments,workdir){
	var schedule = ..com.CreateObject("Schedule.Service");
	schedule.Connect();
			
	var root = schedule.GetFolder("\") 
	if(!taskName) taskName = ..io._exefile;
	try{ root.DeleteTask( taskName ); }
	if(!path){return;}
		
	var taskDef = schedule.NewTask(0);
	taskDef.registrationInfo.Author = "Administrator";
	taskDef.registrationInfo.Description = description;
	taskDef.principal.LogonType = schedule.TASK_LOGON_GROUP;
	taskDef.principal.GroupId = "Builtin\Administrators"
	taskDef.principal.RunLevel = schedule.TASK_RUNLEVEL_HIGHEST; 
 
	taskDef.settings.StartWhenAvailable = true;
	taskDef.settings.Enabled = true 
	taskDef.settings.Hidden = false
	taskDef.settings.DisallowStartIfOnBatteries = false;
	taskDef.settings.StopIfGoingOnBatteries = false;
	                 
	taskDef.settings.RunOnlyIfNetworkAvailable = false;
	taskDef.settings.RunOnlyIfIdle = false;
	taskDef.settings.AllowDemandStart = false;
	taskDef.settings.RestartInterval = "PT1M";
	taskDef.settings.RestartCount = 3;
	taskDef.settings.ExecutionTimeLimit = "PT0S";
	
	var tg = taskDef.triggers.Create(schedule.TASK_TRIGGER_LOGON);
	tg.Enabled = true;                    
    tg.Repetition.StopAtDurationEnd = false;
    tg.ExecutionTimeLimit = "PT0S"
	
	var act = taskDef.actions.Create( schedule.TASK_ACTION_EXEC )
	act.Path = ..io.fullpath(path)
	act.Arguments = ..process.joinArguments(arguments);
	act.WorkingDirectory = workdir || ..io.splitpath(path).dir;
		
	root.RegisterTaskDefinition(taskName, taskDef, schedule.TASK_CREATE_OR_UPDATE, , , schedule.TASK_LOGON_GROUP); 
}

/**intellisense(app)
systemStartup() = 
end intellisense**/
